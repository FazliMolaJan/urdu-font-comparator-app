buildscript {
  repositories {
    jcenter()
        maven { url 'https://maven.fabric.io/public' }
  }
  dependencies {

    //latest version from here https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
    classpath "io.fabric.tools:gradle:$rootProject.fabricGradlePlugin"
  }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
  jcenter()
  maven { url 'https://maven.fabric.io/public' }
}


def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  signingConfigs {
    debug {
      storeFile file("debug.keystore")
    }
    release {
      try {
        keyAlias keystoreProperties['keyAlias']
        keyPassword keystoreProperties['keyPassword']
        storeFile file(keystoreProperties['storeFile'])
        storePassword keystoreProperties['storePassword']
      }
      catch (ex) {
        throw new InvalidUserDataException("You should define required fields in keystore.properties.")
      }
    }
  }
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion
  defaultConfig {
    applicationId "com.androidistan.urdufontcomparator"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode rootProject.ext.versionCode
    versionName rootProject.ext.versionName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    debug {
      minifyEnabled false
      debuggable true
      ext.enableCrashlytics = false
      signingConfig signingConfigs.debug
      applicationIdSuffix ".debug"

      //This is to disable submitting ratings for debug builds to avoid polluting database
      buildConfigField "boolean", "SHOW_RATING_DIALOG", "false"
    }
    release {
      minifyEnabled true
      debuggable false
      ext.enableCrashlytics = true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
      buildConfigField "boolean", "SHOW_RATING_DIALOG", "true"
    }
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')

  // App's support dependencies
  implementation "com.android.support:appcompat-v7:$rootProject.supportLib"
  implementation "com.android.support:support-v4:$rootProject.supportLib"

  // Dependencies for Android UI Tests
  androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.espresso", {
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude module: 'jsr305'
  })
  androidTestImplementation "com.google.dagger:dagger:$rootProject.dagger"

  // Dependencies for local unit tests
  testImplementation "junit:junit:$rootProject.junit"

  // Dependencies for Android unit tests
  androidTestImplementation "junit:junit:$rootProject.junit"

  //UI
  implementation "com.jakewharton:butterknife:$rootProject.butterknife"
  annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknife"

  //Dependency Injection
  implementation "com.google.dagger:dagger:$rootProject.dagger"
  annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger"

  //Dialogs
  implementation "com.github.stfalcon:universalpickerdialog:$rootProject.universalpickerdialog"

  //Lovely dialogue
  implementation "com.yarolegovich:lovely-dialog:$rootProject.lovelydialog"

  //SmileyRating
  implementation "com.github.sujithkanna:smileyrating:$rootProject.smileyrating"

  //Circle Indicator
  implementation "me.relex:circleindicator:$rootProject.circleindicator@aar"

  //Serialization/Deserialization
  implementation "com.google.code.gson:gson:$rootProject.gson"

  //Firebase
  implementation "com.google.firebase:firebase-database:$rootProject.firebase"
  implementation "com.google.firebase:firebase-core:$rootProject.firebase"

  //Logging
  implementation "com.jakewharton.timber:timber:$rootProject.timber"

  // Crashlytics Kit
  releaseImplementation("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlytics@aar") {
    transitive = true
  }
}

//THIS PLUGIN HAS TO BE AT THE BOTTOM
//https://firebase.google.com/docs/android/setup
apply plugin: 'com.google.gms.google-services'